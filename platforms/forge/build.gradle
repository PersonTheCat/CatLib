apply plugin: 'architectury-plugin'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig 'catlib.forge.mixins.json'
        mixinConfig 'catlib.common.mixins.json'
    }
}

dependencies {
    bundle(project(path: "$common", configuration: 'transformProductionForge')) {
        transitive = false
    }
    implementation(project("$common")) {
        transitive = false
    }
    // Add this -> inconsistent class load order.
    // Remove this -> inconsistent memory addresses.
    developmentForge(project("$common")) {
        transitive = false
    }

    forge "net.minecraftforge:forge:$mc_version-$forge_version"
}

jar {
    manifest {
        attributes([
            "Specification-Title": mod_name,
            "Specification-Vendor": mod_vendor,
            "Specification-Version": mod_version,
            "Implementation-Title": mod_id,
            "Implementation-Version": mod_version,
            "Implementation-Vendor": mod_vendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    filesMatching(['**/mods.toml', '**/pack.mcmeta']) {
        expand project.properties
    }
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier 'remapped'
}